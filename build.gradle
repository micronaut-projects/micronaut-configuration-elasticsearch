buildscript {
    repositories {
        jcenter()
        maven { url "https://dl.bintray.com/micronaut/core-releases-local" }
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "io.micronaut.build:micronaut-gradle-plugins:2.0.0.RC11"
    }
}

subprojects { Project subproject ->

    group "io.micronaut.elasticsearch"

    apply plugin: "io.micronaut.build.common"

    if (subproject.parent.name == "docs-examples" || subproject.name == "docs-examples") {
        return
    }

    apply plugin: "io.micronaut.build.dependency-updates"
    if (subproject.name != 'test-suite') {
        apply plugin: "io.micronaut.build.publishing"
    }

    dependencies {
        testImplementation("org.spockframework:spock-core:${spockVersion}") {
            exclude module:'groovy-all'
        }
        testImplementation "io.micronaut.test:micronaut-test-spock:$micronautTestVersion"        
    }

    tasks.withType(Test) {
        systemProperty("elasticsearchVersion", elasticsearchVersion)
        useJUnitPlatform()
    }
}

apply plugin: "io.micronaut.build.docs"
apply plugin: "io.micronaut.build.dependency-updates"

